#!/usr/bin/env zsh

### Get the name of the bundle dir.

# If the bundle is a directory, then we just use that.
# Otherwise, we assume a git repo. For that, by default, use the legacy antibody format:
# $ANTIDOTE_HOME/https-COLON--SLASH--SLASH-github.com-SLASH-zsh-users-SLASH-zsh-autosuggestions
# With `zstyle ':antidote:bundle' use-friendly-names on`, we can simplify to
# $ANTIDOTE_HOME/zsh-users/zsh-autosuggestions

local bundle="$1"

if [[ -d "$bundle" ]]; then
  echo $bundle
elif zstyle -t ':antidote:bundle' use-friendly-names; then
  echo $(_antidote_friendlyname "$bundle")
else
  # sanitize URL for safe use as a dir name
  local url=$(_antidote_tourl $bundle)
  url=${url:gs/\@/-AT-}
  url=${url:gs/\:/-COLON-}
  url=${url:gs/\//-SLASH-}
  echo $(antidote-home)/$url
fi
