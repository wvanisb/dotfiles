#!/usr/bin/env zsh

##? usage: antidote update
##?
##? Updates all previously bundled bundles
##?
##? Flags:
##?   -h, --help           Show context-sensitive help
##?   -v, --version        Show application version

setopt local_options no_notify no_monitor

0=${(%):-%x}

function __antidote_update {
  bundledir="$1"
  local url=$(git -C "$bundledir" config remote.origin.url)
  local oldsha=$(git -C "$bundledir" rev-parse --short HEAD)
  echo "antidote: updating: $url"
  git -C "$bundledir" pull --quiet --ff --rebase --autostash
  local newsha=$(git -C "$bundledir" rev-parse --short HEAD)
  if [[ $oldsha != $newsha ]]; then
    echo "antidote: updated: $url $oldsha -> $newsha"
  fi
}

# update antidote
echo "Updating antidote..."
git -C "${0:A:h:h}" pull --ff --rebase --autostash

echo "Updating bundles..."
local ANTIDOTE_HOME=$(antidote-home)
local d
for d in $ANTIDOTE_HOME/**/.git/..; do
  __antidote_update "${d:A}" &
done
wait
